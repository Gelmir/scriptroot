@ECHO OFF
GOTO BEGIN
:CLEANUP
CD %CWD%
SET INST_DIR=
SET BOOST_ROOT=
IF EXIST %SOURCEROOT%\yaml-cpp RD /S /Q %SOURCEROOT%\yaml-cpp
GOTO END
:FAIL
ECHO Building failed, leaving source tree as is and dumping custom env vars
CD %CWD%
IF DEFINED INST_DIR ECHO INST_DIR = %INST_DIR%
IF DEFINED BOOST_ROOT ECHO BOOST_ROOT = %BOOST_ROOT%
SET INST_DIR=
SET BOOST_ROOT=
GOTO END
:BEGIN
SET "INST_DIR=%BUILDROOT%\yaml-cpp\yaml-cpp64d"
IF EXIST %INST_DIR% RD /S /Q %INST_DIR%
MD %INST_DIR%
CALL %SCRIPTROOT%\virgin.bat backup
SET CWD=%CD%
CALL "C:\Program Files (x86)\Microsoft Visual Studio 10.0\VC\bin\amd64\vcvars64.bat"
IF EXIST %SOURCEROOT%\yaml-cpp RD /S /Q %SOURCEROOT%\yaml-cpp
MD %SOURCEROOT%\yaml-cpp
CD %SOURCEROOT%\yaml-cpp
"C:\Program Files\7-Zip\7z.exe" x T:\_compressed_sources\yaml-cpp-0.3.0.7z -o%SOURCEROOT%\yaml-cpp
MD %SOURCEROOT%\yaml-cpp\build
CD %SOURCEROOT%\yaml-cpp\build
cmake -DBUILD_SHARED_LIBS=ON -DYAML_CPP_BUILD_TOOLS=OFF -D CMAKE_INSTALL_PREFIX:STRING="T:/_outdir/yaml-cpp/yaml-cpp64d" -D CMAKE_BUILD_TYPE:STRING="Debug" -D CMAKE_VERBOSE_MAKEFILE:BOOL=OFF -D CMAKE_CXX_FLAGS:STRING="/favor:blend /EHsc" -D CMAKE_EXE_LINKER_FLAGS:STRING="/INCREMENTAL:NO /NOLOGO" -D CMAKE_MODULE_LINKER_FLAGS:STRING="/INCREMENTAL:NO /NOLOGO" -D CMAKE_SHARED_LINKER_FLAGS:STRING="/INCREMENTAL:NO /NOLOGO" -G "NMake Makefiles" --build .\ ..\
IF ERRORLEVEL 1 GOTO FAIL
nmake
IF ERRORLEVEL 1 GOTO FAIL
nmake install
IF ERRORLEVEL 1 GOTO FAIL
XCOPY /Y /Q /I %SOURCEROOT%\yaml-cpp\build\*.pdb %INST_DIR%\bin\
GOTO CLEANUP
:END
CALL %SCRIPTROOT%\virgin.bat restore